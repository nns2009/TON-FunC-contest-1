;; Each test function must specify method_id
;; Test functions method_id need to started from 0

;; Each test functions must to be in pairs
;; First funtion expect nothing in arguments
;; But need to return:
;;        function selector - which function to test, e.g. 0 for recv_internal, -1 recv_external, 85143 for seqno, and so on
;;        tuple - stack values needed to be passed to function in tuple
;;        c4 cell - start data of smart contract
;;        c7 tuple / null
;;        gas limit integer / null

[int, tuple, cell, tuple, int] test_example_data() method_id(0) {
    ;; Funtion to run (recv_internal)
    int function_selector = 0;

    ;; Example contract message body
    cell message = begin_cell()     ;; external message body
            .store_uint(10, 32)           ;; value to add
            .end_cell();

    tuple stack = unsafe_tuple([message.begin_parse()]); ;; stack that will be passed to function

    cell data = begin_cell()             ;; initial data of contract
        .store_uint(0, 64)               ;; counter
        .end_cell();

    return [function_selector, stack, data, get_c7(), null()];
}


;; After test data function
;; We need to define test funtion
;; It will accept:
;;        exit code - exit code of runvm, here you can check errors / not
;;        c4 cell - data of smart contract after runvm invokes
;;        tuple - stack values of smart contract
;;        c5 cell - here you can check all outcoming messages
;;        gas - gas that was used
;; By default test is passed
;; You can use throw_if to fail test

_ test_example(int exit_code, cell data, tuple stack, cell actions, int gas) method_id(1) {
    throw_if(100, exit_code != 0); ;; test need to be passed

    var ds = data.begin_parse();

    throw_if(101, ds~load_uint(64) != 10); ;; check if counter has been changed
    throw_if(102, gas > 1000000); ;; check if gas usage is not so big
}


;;
;;   GET method test
;;

[int, tuple, cell, tuple, int] test_get_total_data() method_id(2) {
    int function_selector = 128253; ;; funtion to run (128253 is get_total, check contract.fif)
    cell data = begin_cell()             ;; initial data
        .store_uint(10, 64)               ;; counter
        .end_cell();

    return [function_selector, unsafe_tuple([]), data, get_c7(), null()];
}

_ test_get_total(int exit_code, cell data, tuple stack, cell actions, int gas) method_id(3) {
    throw_if(103, exit_code != 0); ;; test need to be passed
    var counter = first(stack); ;; get answer from get method
    throw_if(104, counter != 10); ;; test if answer is 10 as we set
}



;; -------------------- <32 bits incoming message --------------------
[int, tuple, cell, tuple, int] test_short_message_data() method_id(4) {
    int function_selector = 0;
    cell message = begin_cell().store_uint(10, 31).end_cell();
    tuple stack = unsafe_tuple([message.begin_parse()]);
    cell data = begin_cell().store_uint(0, 64).end_cell();

    return [function_selector, stack, data, get_c7(), null()];
}

_ test_short_message(int exit_code, cell data, tuple stack, cell actions, int gas) method_id(5) {
    throw_if(101, exit_code == 0); ;; error needs to happen
}

